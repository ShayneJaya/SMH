CREATE OR REPLACE PROCEDURE PRC_TBL_POLICYDOCUMENTS_VECTOR_STORE(p_start_id IN NUMBER DEFAULT NULL, p_end_id IN NUMBER DEFAULT NULL) IS
    TYPE t_id_tab IS TABLE OF TBL_POLICYDOCUMENTS.id%TYPE INDEX BY PLS_INTEGER;
    v_ids t_id_tab;
    v_cursor SYS_REFCURSOR;
    v_id TBL_POLICYDOCUMENTS.id%TYPE;
BEGIN
    IF p_start_id IS NULL AND p_end_id IS NULL THEN
        OPEN v_cursor FOR 
            SELECT id 
            FROM TBL_POLICYDOCUMENTS;
    ELSE
        OPEN v_cursor FOR 
            SELECT id 
            FROM TBL_POLICYDOCUMENTS
            WHERE id BETWEEN p_start_id AND p_end_id;
    END IF;

    LOOP
        FETCH v_cursor INTO v_id;
        EXIT WHEN v_cursor%NOTFOUND;

        INSERT INTO vector_store (doc_id, embed_id, embed_data, embed_vector)
        SELECT dt.id AS doc_id, 
              et.embed_id, 
              et.embed_data, 
              to_vector(et.embed_vector) AS embed_vector
        FROM TBL_POLICYDOCUMENTS dt
        CROSS JOIN TABLE(
            dbms_vector_chain.utl_to_embeddings(
                dbms_vector_chain.utl_to_chunks(
                    dbms_vector_chain.utl_to_text(dt.filecontent), 
                    json('{"by":"words","max":"300","split":"sentence","normalize":"all"}')
                ),
                json('{"provider":"database", "model":"MiniLM_L12_v2"}')
            )
        )  t
        CROSS JOIN JSON_TABLE(
            t.column_value, 
            '$[*]' COLUMNS (
                embed_id NUMBER PATH '$.embed_id',
                embed_data VARCHAR2(4000) PATH '$.embed_data',
                embed_vector CLOB PATH '$.embed_vector'
            )
        ) AS et
        WHERE dt.id = v_id;

    END LOOP;

    CLOSE v_cursor;
END PRC_TBL_POLICYDOCUMENTS_VECTOR_STORE;
/
